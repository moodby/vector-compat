apply plugin: 'maven-publish'
apply plugin: 'signing'

project.afterEvaluate {
    publishing {
        publications {
            lib(MavenPublication) {
                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                if (project.hasProperty("android")) {
                    artifact bundleReleaseAar
                    artifact androidSourcesJar
                    artifact androidJavadocsJar
                } else {
                    artifact jar
                    artifact sourcesJar
                    artifact javadocJar
                }

                pom {
                    name = libraryName
                    artifactId = artifact
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }

                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }

                    scm {
                        url = siteUrl
                        connection = gitUrl
                        developerConnection = gitUrl
                    }
                }
                //region Add your description here
                pom.withXml {
                    final dependenciesNode = asNode().appendNode('dependencies')

                    //region declare fun addDependency(dep, scope)
                    ext.addDependency = { Dependency dep, String scope ->
                        if (dep.group == null
                                || dep.version == null
                                || dep.name == null
                                || dep.name == "unspecified")
                            return // ignore invalid dependencies

                        final dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dep.group)
                        dependencyNode.appendNode('artifactId', dep.name)
                        dependencyNode.appendNode('version', dep.version)
                        dependencyNode.appendNode('scope', scope)

                        if (!dep.transitive) {
                            // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                            final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                            exclusionNode.appendNode('artifactId', '*')
                            exclusionNode.appendNode('groupId', '*')
                        } else if (!dep.properties.excludeRules.empty) {
                            // Otherwise add specified exclude rules
                            final exclusionsNode = dependencyNode.appendNode('exclusions')
                            dep.properties.excludeRules.each { ExcludeRule rule ->
                                def exclusion = exclusionsNode.appendNode('exclusion')
                                exclusion.appendNode('artifactId', rule.module ?: '*')
                                exclusion.appendNode('groupId', rule.group ?: '*')
                            }
                        }
                    }
                    //endregion

                    def apiDependencies = configurations.api.getAllDependencies()
                    def implementationDependencies = configurations.implementation.getAllDependencies()

                    implementationDependencies.each { dep ->
                        if (!apiDependencies.contains(dep)) {
                            addDependency(dep, "runtime")
                        }
                    }

                    apiDependencies.each { dep ->
                        addDependency(dep, "compile")
                    }

                }
                //endregion
            }
        }

        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                url = uri(project.findProperty("nexus.url"))
                name = 'mavenNexus'
                credentials {
                    username project.findProperty("nexus.user")
                    password project.findProperty("nexus.password")
                }
            }
        }
    }
}